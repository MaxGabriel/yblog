#!/usr/bin/env zsh

checkfile=".last_multi_watched"

[[ -e $checkfile ]] && \
(( $(find $checkfile -mmin -1 | wc -l ) > 0)) && {
    print -- "watching in progress"
    exit 0
}

ONCE=0
[[ $1 = "once" ]] && { ONCE=1 }

packageconf="$(print -- cabal-dev/packages-*.conf(N))"
[[ $packageconf = "" ]] && {
    print -- "Please, wait the end of compilation and relaunch preview again"
    exit 1
}

languages=( $(runghc -package-conf=$packageconf showlangs.hs) )
print "languages to watch (in Config.hs) are: $languages"
typeset -a exclu

while true; do
  for language in $languages; do
      exclu=()
    for l in $languages; do
      case $l in
        $language) continue ;;
      esac
      exclu=($exclu $l)
    done
    for fic in multi/**/*(.); do
      dest=Scratch/$language/${fic#multi/}
      [[ ! -e $dest || $fic -nt $dest ]] || continue
      print $dest
      [[ ! -d ${dest:h} ]] && mkdir ${dest:h}
      excluregexp="${(j:|:)exclu}"
      awkprg='! /^('$excluregexp'): / { sub(/^'$language': /,""); print $0 }'
      awk "$awkprg" < $fic > $dest
    done
  done
  ((ONCE == 1)) && break
  touch $checkfile
  sleep 2
done
